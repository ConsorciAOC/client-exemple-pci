plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.6.3'
}

group 'cat.aoc.client_pci'
version '1.0-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom('org.springframework.boot:spring-boot-dependencies:2.6.3')
    }
}

configurations {
    jaxb
}

dependencies {
    implementation group: 'jakarta.activation', name: 'jakarta.activation-api', version: '2.0.1'

    implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '3.0.1' // JAXB API
    implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '3.0.1' // JAXB reference implementation (curiously with com.sun coordinates)
    implementation group: 'com.sun.xml.bind', name: 'jaxb-core', version: '3.0.1'

    // necessary for generating JAVA classes with gradle 'jaxb' task
    jaxb(
            'com.sun.xml.bind:jaxb-impl:3.0.1', // JAXB Impl
            'com.sun.xml.bind:jaxb-xjc:3.0.1', // XJC tools
            'jakarta.xml.bind:jakarta.xml.bind-api:3.0.1',
            'jakarta.activation:jakarta.activation-api:2.0.1'
    )

    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'org.springframework.ws:spring-ws-security'

    // Lombok
    compileOnly('org.projectlombok:lombok:1.18.22')
    annotationProcessor('org.projectlombok:lombok:1.18.22')

    // Testing dependencies
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.1'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.1'

}

test {
    useJUnitPlatform()
}

task jaxb {
    description 'Convert XSD\'s to java classes'
    def srcDir = file("src/main/java/")
    def xsdParentDir = file("src/main/resources/xsd/")
    // Restricts access to the protocols specified for external references set by the schemaLocation attribute, import element, and include element.
    System.setProperty('javax.xml.accessExternalDTD', 'all')
    System.setProperty('javax.xml.accessExternalSchema', 'all')
    ant.taskdef(
            name: 'xjc',
            classname: 'com.sun.tools.xjc.XJCTask',
            classpath: configurations.jaxb.asPath
    )
    ant.jaxbTargetDir = srcDir
    doLast {
        xsdParentDir.eachDir(xsdProjectDir -> {
            def packageName = ('generated.' + xsdProjectDir.name)
            def projectWithBindings = !fileTree(xsdProjectDir.getAbsolutePath()).filter { file ->
                file.name.contains('.xjb')
            }.isEmpty()
            if (projectWithBindings) {
                println('Genearating source code from: ' + xsdProjectDir.name + ' (bindings found)')
                ant.xjc(
                        destdir: srcDir,
                        package: packageName){
                    schema(dir: xsdProjectDir.getAbsoluteFile(), includes: "**/*.xsd")
                    binding(dir: xsdProjectDir.getAbsoluteFile(), includes: "**/*.xjb")
                    arg(value: "-extension")
                }
            } else {
                println('Genearating source code from: ' + xsdProjectDir.name)
                ant.xjc(
                        destdir: srcDir,
                        package: packageName){
                    schema(dir: xsdProjectDir.getAbsoluteFile(), includes: "**/*.xsd")
                }
            }
        })
        // WSDL Compilation
        ant.xjc(
                destdir: srcDir,
                schema: 'src/main/resources/wsdl/sincron.wsdl',
                language: 'WSDL'
        )
    }
}

compileJava {options.encoding = "ISO-8859-1"}
compileJava.dependsOn jaxb

